plugins {
  id 'java'
}

group 'dev.fiki.forgehax.javac'

sourceSets {
  templates {
    compileClasspath += main.compileClasspath

    // don't compile any java files
    // there is probably a better way to do this... probably
    java {
      exclude '*'
    }
  }
}

ext {
  unpackedLombok = file("$project.buildDir/lombok")
}

configurations {
  lombok
}

repositories {
  mavenCentral()
}

dependencies {
  lombok group: 'org.projectlombok', name: 'lombok', version: '1.18.16'

//  compile group: 'org.kohsuke.metainf-services', name: 'metainf-services', version: '1.8'

  implementation files("${System.properties['java.home']}/../lib/tools.jar")
  implementation project(':Annotations')
  implementation files(unpackedLombok)

//  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.16'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.0'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'
  testImplementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
  testImplementation group: 'com.google.testing.compile', name: 'compile-testing', version: '0.19'
  testImplementation group: 'org.ow2.asm', name: 'asm-debug-all', version: '5.2'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.18.1'
}

task unpackLombok {
  inputs.files configurations.lombok
  outputs.dir unpackedLombok

  doLast {
    copy {
      from zipTree(configurations.lombok.singleFile)
      into unpackedLombok
      include 'lombok/**/*'
      rename "(.*)[.]SCL[.]lombok", '$1.class'
      duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
  }
}

test {
  dependsOn unpackLombok

  environment 'BUILD_DIR', project.buildDir
  environment 'TEMPLATES_DIR', sourceSets.templates.java.srcDirs[0].getAbsolutePath()
  environment 'REMAPPER_FILE', file('src/test/resources/mappings').getAbsolutePath()
  useJUnitPlatform()
}

tasks.compileJava.dependsOn unpackLombok
